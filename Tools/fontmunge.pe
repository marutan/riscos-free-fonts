#!/usr/bin/fontforge
Open($1)
#
# Generate some acceptable TTF names for this font
names = StrSplit($fontname, ".");
fullname = StrJoin(names, " ");
subfamily = StrSplit(Strsub($fontname, Strstr($fontname, ".") + 1), ".")
family = names[0];
fontname = family + "-" + StrJoin(subfamily, "");

# If this is an Oblique font, then skew it at a reasonable angle
if (Strstr($1, "Oblique") != -1)
	SelectAll();
	Skew(12)
endif

# Set the italic angle
if (Strstr($1, "Italic") != -1)
	SetItalicAngle(-15.5)
endif

# Fix the winding rule issue with font plotting (filled in 'o' 'e' 'B' etc)
SelectAll()
CorrectDirection()

# Without this, some of the hinting in Homerton Bold for example looks terrible!
ClearHints()

# Add in the copyright string
SetFontNames(fontname,family,fullname,"","(c) RISC OS Developments Ltd, released under Apache License 2.0","1");

# Set the StyleMap to help Windows decide which fonts are in a family
if (Strstr($1, "Bold.Italic") != -1)
	SetOS2Value("StyleMap", 0x21);
elseif (Strstr($1, "Bold.Oblique") != -1)
	SetOS2Value("StyleMap", 0x21);
elseif (Strstr($1, "Bold") != -1)
	SetOS2Value("StyleMap", 0x20);
elseif (Strstr($1, "Italic") != -1)
	SetOS2Value("StyleMap", 0x01);
elseif (Strstr($1, "Oblique") != -1)
	SetOS2Value("StyleMap", 0x01);
else
	SetOS2Value("StyleMap", 0x40);
endif

# Stuff has changed for the better, save changes to the sfd
Save()

# Make a nice shiney otf to actually use :)
Generate($1:r + ".otf")
